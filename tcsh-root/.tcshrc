# September 17 2021
#

###########
## Alias ##
###########

# cd
alias cd..        cd ..
alias ..          cd ..
alias ...         cd ../../
alias ....        cd ../../../
alias .....       cd ../../../../
alias ......      cd ../../../../../

alias ..2         cd ../..
alias ..3         cd ../../..
alias ..4         cd ../../../..
alias ..5         cd ../../../../..

# chmod
alias mx          chmod a+x
alias 000         chmod -R 000
alias 644         chmod -R 644
alias 666         chmod -R 666
alias 755         chmod -R 755
alias 777         chmod -R 777

# ls
alias l          'ls -lAFGh -D ""'
alias ls          ls -AFG
alias lsgz        tar -tzf
alias lstar       tar -tzf
alias ls7z        7z l
alias lszip       unzip -l

# Misc
alias b           bastille
alias cp          cp -i
alias dots       'cd ~/.dotfiles'
alias du          du -h 
alias e          '$EDITOR'
alias se         'sudo $EDITOR'
alias grep        grep --color=auto
## Search command line history
alias h          'history | grep '
alias mv          mv -i
alias nuke        rm -rf
## Search running processes
alias p          'ps aux | grep '
alias q           exit
alias Q           exit
alias :q          exit
alias :Q          exit
alias renew      'source ~/.tcshrc'
alias rm          rm -iv
alias s          'sudo '
alias tmux       'tmux -2u -f ~/.config/tmux/.tmux.conf'
alias tmuxnew    'tmux -2u -f ~/.config/tmux/.tmux.conf new-session -A -s tmux-main'
alias whatsmyip	 'host -4 myip.opendns.com resolver1.opendns.com && host -6 myip.opendns.com resolver1.opendns.com'

# A righteous umask
umask 22

#########################
## Path & Applications ##
#########################
set path = (/sbin /bin /usr/sbin /usr/bin /usr/local/sbin /usr/local/bin $HOME/.local/bin)

#############
## Exports ##
#############
setenv LANG en_US.UTF-8
setenv LESS '-FiRMSQ~ --mouse --wheel-lines 2 --shift 5'
setenv LESSHISTFILE - #Disable .lesshst file
setenv LESSSECURE 1
setenv EDITOR nvim
setenv MANPAGER 'nvim +Man! -c ":set laststatus=0 nowrap nocursorline nocursorcolumn"'
setenv PAGER less
setenv BLOCKSIZE K
limit  coredumpsize 0
setenv XDG_CONFIG_HOME $HOME/.config

##########################
## An interactive shell ##
##########################
if ($?prompt) then
    # History
    set history     = 5000
    set savehist    = (5000 merge)
    set histdup     = erase
    # History is immediately stored, paralle shells share history   
    alias precmd 'history -S'
    alias postcmd 'history -M'
    # Keeping history in stock location for root
    set histfile    = ~/.history

    # Completion Settings
    set complete    = enhance
    set autoexpand                       # Use history to aid expansion
    set filec                            # Auto correct with tab
    set autolist    = ambiguous
    set addsuffix                        # distinguishes between files and directories /
    set autocorrect = ambiguous
    set symlinks    = chase              # symbolic link resolution
    # * matches files starting with a .
    set globdot

    # Allow the wildcard '**' to recurse into subdirectories
    set globstar

    # Misc
    set correct     = cmd                # correct for mistyped commands
    set noding                           # disable "Ding!" in the prompt time
    set autorehash  = always             # auto rehash after pkg install
    set watch       = (0 any any)        # notify when someone logs in or out
    set who         = "%n %a %l from %m" # format for watch messages
    set rmstar                           # user is prompted before `rm *' is executed
    set mail        = (/var/mail/$USER)

    ############
    ## Colour ##
    ############
    set color 
    set colorcat
    setenv CLICOLOR "true"
    setenv LSCOLORS "exfxcxdxbxegedabagacad"
    # Colours for prompt
    set black       = "%{\033[0;30m%}"
    set grey        = "%{\033[0;37m%}"
    set red         = "%{\033[1;31m%}"
    set green       = "%{\033[32;1m%}"
    set yellow      = "%{\033[1;33m%}"
    set blue        = "%{\033[1;34m%}"
    set magenta     = "%{\033[0;35m%}"
    set purple      = "%{\033[34;1m%}"
    set purplel     = "%{\033[1;35m%}"
    set cyan        = "%{\033[1;36m%}"
    set white       = "%{\033[1;37m%}"
    set clear       = "%{\033[0m%}"

    #############
    ## Prompts ##
    #############

    # Prompt 2 line no rprompt
    set prompt      = "${red}%N@%m:${purple}%/\n${magenta}%T ${green}❭${clear} "

    # Prompt single line rprompt
    #set prompt   = "${red}%m:${purple}[%/] ${red}❭${clear} "
    # right prompt TIME %P = seconds %T = no seconds
    #set rprompt  = "${clear}[${magenta}%T${clear}]${clear}"

    # Unset colours for prompt
    unset grey red green yellow blue purple magenta cyan white clear

    ##################
    ## Key bindings ##
    ##################

    if ( $?tcsh ) then
        bindkey "^W" backward-delete-word
        bindkey -k up history-search-backward
        bindkey -k down history-search-forward
        bindkey "\e[1~" beginning-of-line          # home
        bindkey "\e[2~" overwrite-mode             # insert
        bindkey "\e[3~" delete-char                # delete
        bindkey "\e[4~" end-of-line                # end
        bindkey "\e[1;5D" backward-word            # Ctrl-LEFT
        bindkey "\e[1;5C" forward-word             # Ctrl-RIGHT
        bindkey "\e[1;5A" beginning-of-line        # Ctrl-UP
        bindkey "\e[1;5B" end-of-line              # Ctrl-DOWN
    endif

    #################
    ## Completions ##
    #################
    # https://github.com/tcsh-org/tcsh/blob/master/complete.tcsh

    # Directories only
    complete cd         p/1/d/
    complete rmdir      p/1/d/

endif
